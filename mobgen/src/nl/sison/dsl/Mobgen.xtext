grammar nl.sison.dsl.Mobgen with org.eclipse.xtext.common.Terminals

/*
 * 
 * Handy links:
 * * http://pettergraff.blogspot.nl/2009/12/deploying-code-generator-with.html
 * * http://charandeepmatta.com/2012/09/01/how-to-generate-eclipse-plugin-for-your-dsl-written-in-xtext/ - sharing your plugin
 * * http://www.techjava.de/topics/2010/06/extending-xtext-build-participants/ - using "Nature" to distinguish DSLs
 * * http://stackoverflow.com/questions/10917386/linking-xtext-with-stringtemplate-code-generator - run the generator from Main.java
 * * http://wiki.eclipse.org/Xtext/FAQ#How_do_I_load_my_model_in_a_standalone_Java_application.C2.A0.3F - run xtend generation as standalone, e.g. from main method
 * * http://davehofmann.de/blog/?p=101 // - standalone way of setting up the generator
 * * http://koehnlein.blogspot.de/2012/11/xtext-tip-how-do-i-get-guice-injector.html - Guice and standalone dependency injection
 * * http://christiandietrich.wordpress.com/2011/10/15/xtext-calling-the-generator-from-a-context-menu/ - Running "Generate code" from the context menu
 * 
 * IDEA name for the project, plate smasher (boilerplate smasher)
 * IDEA feed vector graphics, generate assets l, m, h, xh, xxh, xxxh, pixel dimensions
 * 
 * -JS (06-Mar-2014)
 * 
 */

generate mobgen "http://www.sison.nl/dsl/Mobgen"

/**
 * 
 * Mobgen http call generator parser
 * 
 */
Mobgen:
	platform=Platform
	(	
		resources+=MobgenResourceDefinition | calls+=MobgenCallDefinition	
	)+
;

Platform:
	'platform'
	'{'
		(platforms+=ID '->' generateWhere+=STRING)+
	'}'
;

/**
 * Only POST / PUT request method can do a 'server' 'expects' 
 */
MobgenCallDefinition:
	'call' name=ID ':' method=RESTFUL_METHODS 'from' uri=URI
	('with' 'headers' 'from' 'request' requestHeaders=MobgenHeader
		('and' 'response' responseHeaders=MobgenHeader)?
	)?
	('client' 'expects' jsonToClient=MobgenJson)?
	('server' 'expects' jsonToServer=MobgenJson)?
;

URI:
	(ANY_OTHER)+ ('{' parameters+=ID '}'(ANY_OTHER)*)*
;

MobgenHeader:
	(name=ID)?
	"'''" 
	'{' (headerKeyValues+=MobgenHeaderKeyValuePair)
		(',' headerKeyValues+=MobgenHeaderKeyValuePair )* (',')? '}'
	"'''"
;

MobgenHeaderKeyValuePair:
	(key=STRING) '=' (value=STRING | value=MOBGEN_HEADER_PARAMETER)	
;

terminal MOBGEN_HEADER_PARAMETER:
	'{' ID '}'
;

terminal RESTFUL_METHODS:
	'PUT' | 'POST' | 'GET' | 'DELETE' // TODO HEAD | TRACE etc. will be automagically generated as well, a body-less response
;

MobgenJson:
	((name=ID)
		"'''" 
		value=JsonObjectValue
		"'''"
	) 
;

/**
 *  Borrowed from http://www.ebpml.org/blog2/index.php/2010/11/05/mde-xtext-and-json
 */
JsonObjectValue:
	value=JsonLiteralValue | composite=JsonCompositeValue
;

JsonCompositeValue:
	JsonObject | JsonArray
;

JsonLiteralValue:
	STRING | EMPTY_JSON_OBJECT | EMPTY_JSON_ARRAY | JSON_LITERAL_BOOLEAN | JSON_LITERAL_NULL |  JSON_NUMBER | JSON_META_SCALAR_TYPE
;

JsonObject:
	'{'
		keyValuePair+=JsonKeyValuePair (',' keyValuePair+=JsonKeyValuePair)* (',')?
	'}'
;

JsonKeyValuePair:
	key=STRING ':' value=JsonObjectValue
;

JsonArray:
	'['items+=JsonObjectValue (',' items+=JsonObjectValue)* (',')? ']'
;

terminal JSON_META_SCALAR_TYPE:
	'boolean' | 'number' | 'string'
;

terminal EMPTY_JSON_OBJECT:
	'{}'
;

terminal EMPTY_JSON_ARRAY:
	'[]'
;

terminal JSON_LITERAL_NULL: 'null';

terminal JSON_LITERAL_BOOLEAN:
	'true' | 'false'
;

terminal JSON_NUMBER:
	('0' | '1..9'+ INT) ('.' INT)?
;

/**
 * 
 * Mobgen resource generator
 * 
 */
 
MobgenResourceDefinition:
	resources += MapInstance | EnumInstance
;


EnumInstance:
	'enum' name=ID
	'{'
		(values += ID) (',' values += ID)* (',')?
	'}'	
;

MapInstance:
	'map' name=ID
	'{'
		(keys+=ID ':' values+=NestedType) (',' keys+=ID ':' values+=NestedType)* (',')?
	'}' // you must have at least one map
;

StringList:
	'[' (values += STRING) (',' values += STRING)* (',')? ']'
;

NestedType:
	list=StringList | string=STRING
;
